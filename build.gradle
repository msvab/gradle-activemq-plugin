apply plugin: 'groovy'
apply plugin: 'idea'

version = '0.1'
defaultTasks 'clean', 'build', 'jar'

sourceCompatibility = '1.5'
targetCompatibility = '1.5'

repositories {
    mavenCentral()
}

dependencies {
    compile gradleApi()
    groovy 'org.codehaus.groovy:groovy:2.0.4'

    compile('org.apache.activemq:activemq-core:5.6.0') {
        exclude module: 'activemq-protobuf'
        exclude module: 'fusemq-leveldb'
        exclude module: 'mqtt-client'
        exclude module: 'org.osgi.core'
        exclude module: 'jasypt'
    }

    testCompile 'junit:junit:4.11-beta-1'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
    testCompile 'org.mockito:mockito-core:1.9.5'
}

idea.project {
    jdkName = '1.6'

    ipr.withXml { provider ->
        def node = provider.asNode()

        // Use GIT
        def vcsConfig = node.component.find { it.'@name' == 'VcsDirectoryMappings' }
        vcsConfig.mapping[0].'@vcs' = 'Git'

        // Set Gradle home
        def gradleSettings = node.appendNode('component', [name: 'GradleSettings'])
        gradleSettings.appendNode('option', [name: 'SDK_HOME', value: gradle.gradleHomeDir])
    }
}

manifest {
    attributes 'Implementation-Title': 'Gradle ActiveMQ plugin',
            'Implementation-Version': version,
            'Built-By': System.getProperty('user.name'),
            'Built-Date': new Date(),
            'Built-JDK': System.getProperty('java.version')
}